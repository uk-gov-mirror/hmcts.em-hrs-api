
buildscript {
  dependencies {
    classpath("org.flywaydb:flyway-database-postgresql:11.1.1")
  }
}

plugins {
  id 'application'
  id 'checkstyle'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.springframework.boot' version '3.4.1'
  id 'com.github.ben-manes.versions' version '0.51.0'
  id 'org.sonarqube' version '6.0.1.5171'
  id 'org.flywaydb.flyway' version '11.1.1'
  id 'au.com.dius.pact' version '4.6.16'
  id "info.solidsoft.pitest" version '1.15.0'
  id 'com.github.hmcts.rse-cft-lib' version '0.19.1528'
  id 'uk.gov.hmcts.java' version '0.12.63'
  id 'org.owasp.dependencycheck' version '11.1.1'
  id 'net.serenity-bdd.serenity-gradle-plugin' version '4.2.11'
}


java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

group = 'uk.gov.hmcts.reform.em.hrs'
version = '0.0.1'
mainClassName = 'uk.gov.hmcts.reform.em.hrs.Application'

/***********************************************************************************************************************
 * Source Sets
 **********************************************************************************************************************/

sourceSets {
  //functional tests typically run on AAT environment only. Includes tests to real external endpoints.
  //if running locally, need the relevant docker containers to test against
  //if need to run any locally, then the specific test(s) should be specified
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

  //integrationTest typically tests with mocked external endpoints (so can run locally without dependencies)
  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

  //contractTest typically tests with PACT broker
  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

  //smokeTest should include helloworld & healthcheck. Ideally will also include downstream healthchecks
  //should work locally with docker dependencies, in AAT and production
  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

  test {
    java.srcDir 'src/test/java'
    resources.srcDir file('src/test/resources')
  }
}

/***********************************************************************************************************************
 * Build Tasks
 **********************************************************************************************************************/

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

task functional(type: Test) {
  testLogging {
    events = ["passed", "failed", "skipped"]
    showStandardStreams = true
  }
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
  include "uk/gov/hmcts/reform/em/hrs/**"
  logger.info("Functional tests info:")
  logger.info('A {} log message', 'info')
}



task integration(type: Test) {
  failFast = true
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
}

task smoke(type: Test) {
  failFast = true
  group = 'Delivery pipeline'
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath

  environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")

}

/***********************************************************************************************************************
 * Code Checks
 **********************************************************************************************************************/

def coverageExclusionList = [
  '**uk/gov/hmcts/reform/em/hrs/Application*',
  '**uk/gov/hmcts/reform/em/hrs/appinsights/**',
  '**uk/gov/hmcts/reform/em/hrs/config/**',
  '**uk/gov/hmcts/reform/em/hrs/domain/**',
  '**uk/gov/hmcts/reform/em/hrs/dto/**',
  '**uk/gov/hmcts/reform/em/hrs/exception/**',
  '**uk/gov/hmcts/reform/em/hrs/job/*Scheduler.java',
  '**uk/gov/hmcts/reform/em/hrs/model/**',
  '**uk/gov/hmcts/reform/em/hrs/storage/HearingRecordingStorageImpl.java',
  '**uk/gov/hmcts/reform/em/hrs/util/debug/HttpHeadersLogging.java',
  '**uk/gov/hmcts/reform/em/hrs/data/migration/PendingMigrationScriptException*',
  '**uk/gov/hmcts/reform/em/hrs/data/migration/FlywayNoOpStrategy*',
  '**uk/gov/hmcts/reform/em/hrs/job/UpdateJurisdictionCodes*'
]

checkstyle {
  maxWarnings = 0
  toolVersion = '10.21.1'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

jacocoTestReport {
  dependsOn test, integration
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }

  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: coverageExclusionList)
    }))
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: em-hrs-api"
    property "sonar.projectKey", "uk.gov.hmcts.reform.em.hrs:em-hrs-api"
    property "sonar.exclusions", coverageExclusionList.join(", ")
    property "sonar.coverage.jacoco.xmlReportPaths", jacocoTestReport.reports.xml.outputLocation
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.cpd.exclusions", "**/*Scheduler.java"
  }
}

/***********************************************************************************************************************
 * Dependency Management
 **********************************************************************************************************************/




// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

configurations.all {
  exclude group: 'org.springframework.security', module: 'spring-security-rsa'
  exclude group: 'org.springframework.boot', module: 'spring-boot-starter-security'
  exclude group: 'com.vaadin.external.google',   module: 'android-json'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
  maven { url  'https://repo.spring.io/milestone/'}
}

def versions = [
  serenity           : '4.2.11',
  lombok             : '1.18.36',
  reformLogging      : '6.1.7',
  log4JVersion       : "2.17.1",
  postgresql         : '42.7.4',
  pact_version       : '4.6.16',
  restAssured        : '5.3.0',
  junit              : '5.11.4',
  junitPlatform      : '1.11.4',
  springSecurity     : '6.4.2',
  shedlock           : '6.0.2',
  testcontainers     : '1.20.4'
]

dependencies {

  def withoutStuff = {
    exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-security'
    exclude group: 'uk.gov.hmcts.reform', module: 'java-logging-spring'
  }

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail'

  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server', version: versions.springSecurity
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-client', version: versions.springSecurity
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose', version: versions.springSecurity
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-core', version: versions.springSecurity
  implementation group: 'org.springframework.security', name: 'spring-security-config', version: versions.springSecurity
  implementation group: 'org.springframework.security', name: 'spring-security-core', version: versions.springSecurity
  implementation group: 'org.springframework.security', name: 'spring-security-web', version: versions.springSecurity

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '4.2.0'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.2.0'


  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-starter-json"
  implementation "org.springframework.boot:spring-boot-starter-validation"
  implementation "org.springframework.boot:spring-boot-starter-web"

//hmcts autho
  implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '3.0.3'
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.3.0'

//hmcts ccd
  implementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '5.0.3'

//hmcts logging
  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.reformLogging
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-spring', version: '5.1.9'

//gov notify email service
  implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '5.2.1-RELEASE'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.8.1'
//azure components
  implementation group: 'com.azure', name: 'azure-storage-blob', version: '12.29.0'
  implementation group: 'com.azure', name: 'azure-identity', version: '1.14.2'

  // Job scheduling
  implementation group: 'org.quartz-scheduler', name: 'quartz', version: '2.5.0'
  implementation group: 'org.springframework', name: 'spring-context-support'

  implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: versions.shedlock
  implementation group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc-template', version: versions.shedlock

  implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.12.0'
  implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.4.0'


  cftlibImplementation 'org.springframework.boot:spring-boot-devtools'

/***********************************************************************************************************************
 * DATABASE DEPENDENCIES AND SOURCE SETS
 **********************************************************************************************************************/

  implementation group: 'org.postgresql', name: 'postgresql', version: versions.postgresql
  implementation group: 'org.flywaydb', name: 'flyway-core', version: '11.1.1'
  runtimeOnly group: 'org.flywaydb', name: 'flyway-database-postgresql', version: '11.1.1'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  implementation "org.springframework.boot:spring-boot-starter-jdbc"

  testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity, withoutStuff
  testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity, withoutStuff
  testImplementation group: 'net.serenity-bdd', name: 'serenity-junit5', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-single-page-report', version: versions.serenity
  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.4.6', classifier: 'all'

/***********************************************************************************************************************
 * TEST DEPENDENCIES
 **********************************************************************************************************************/

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions.junit
  testImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
  testImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform
  functionalTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions.junit
  functionalTestImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
  functionalTestImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  functionalTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  functionalTestImplementation 'com.github.hmcts:em-test-helper:2.0.9'
  functionalTestImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity, withoutStuff

  testImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: '1.1.2'

  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.2'
  testImplementation group: 'net.javacrumbs.json-unit', name: 'json-unit-assertj', version: '4.1.0'

  testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: versions.testcontainers
  testImplementation group: 'org.testcontainers', name: 'postgresql', version: versions.testcontainers

  testImplementation group: 'com.devskiller', name: 'jfairy', version: '0.6.5', {
    exclude group: 'com.google.guava', module: 'guava'
  }
  testImplementation group: 'org.awaitility', name: 'awaitility', version: '4.2.2'

  functionalTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
  functionalTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.4'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.4'


  functionalTestImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity, withoutStuff
  functionalTestImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity, withoutStuff
  functionalTestImplementation group: 'net.serenity-bdd', name: 'serenity-junit5', version: versions.serenity
  functionalTestImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
  functionalTestImplementation group: 'net.serenity-bdd', name: 'serenity-single-page-report', version: versions.serenity

  smokeTestImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity, withoutStuff
  smokeTestImplementation group: 'net.serenity-bdd', name: 'serenity-junit5', version: versions.serenity
  smokeTestImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity, withoutStuff
  smokeTestImplementation group: 'net.serenity-bdd', name: 'serenity-single-page-report', version: versions.serenity
  smokeTestImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
  smokeTestImplementation group: 'com.github.hmcts', name: 'em-test-helper', version: '2.0.9'


/***********************************************************************************************************************
 * TEST SOURCE SETS
 **********************************************************************************************************************/

  functionalTestImplementation sourceSets.main.runtimeClasspath

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath

  contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit', version: versions.pact_version
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version

  contractTestImplementation "org.junit.jupiter:junit-jupiter-api:5.11.4"
  contractTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.11.4"
  contractTestImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: versions.junitPlatform

  contractTestImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath

  contractTestImplementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
  contractTestImplementation group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.14'
}

processFunctionalTestResources {
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

processIntegrationTestResources {
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

processTestResources {
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
/***********************************************************************************************************************
 * Adhoc
 **********************************************************************************************************************/
test {
  useJUnitPlatform()
}

bootJar {
  getArchiveFileName().set(provider {
    'em-hrs-api.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

task contract(type: Test) {
  group = 'Delivery pipeline'
  description = 'Runs the consumer Pact tests'
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  include "uk/gov/hmcts/reform/em/hrs/consumer/**"
  systemProperty 'pact.rootDir', "pacts"
  environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

task runAndPublishConsumerPactTests(type: Test){
  logger.lifecycle("Runs pact Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath

}

runAndPublishConsumerPactTests.dependsOn contract

runAndPublishConsumerPactTests.finalizedBy pactPublish

project.ext {
  pactVersion = getCheckedOutGitCommitHash()
}

static def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

task loadEnvSecrets() {
  doLast {
    if (!project.file('./.aat-env').exists()) {
      new ByteArrayOutputStream().withStream { os ->
        exec {
          commandLine 'az', 'keyvault', 'secret', 'show', '--vault-name', 'em-hrs-api-aat', '-o', 'tsv', '--query', 'value', '--name', 'em-hrs-api-dot-env'
          standardOutput = os
        }
        project.file('./.aat-env').write(new String(os.toString().replace('\n', '').decodeBase64(), java.nio.charset.StandardCharsets.UTF_8))
      }
    }
  }
}

task reloadEnvSecrets(group: 'ccd tasks', dependsOn: [loadEnvSecrets]) {
  doFirst {
    if (project.file('./.aat-env').exists()) {
      project.file('./.aat-env').delete()
    }
  }
}

bootRun {
  doFirst() {
    if (project.file('./.aat-env').exists()) {
      project.file('./.aat-env').readLines().each() {
        def index = it.indexOf("=")
        def key = it.substring(0, index)
        def value = it.substring(index + 1)
        environment key, value
      }
    }
  }
}

functional {
  doFirst() {
    if (project.file('./.aat-env').exists()) {
      project.file('./.aat-env').readLines().each() {
        def index = it.indexOf("=")
        def key = it.substring(0, index)
        def value = it.substring(index + 1)
        environment key, value
      }
    }
  }
}

bootWithCCD {
  dependsOn(loadEnvSecrets)

  doFirst() {
    project.file('./.aat-env').readLines().each() {
      def index = it.indexOf("=")
      def key = it.substring(0, index)
      def value = it.substring(index + 1)
      environment key, value
    }

    exec {
      commandLine 'docker', 'compose', '-f', 'src/cftlib/resources/docker-compose-local.yml', '-p', 'cftlib', 'up', '-d'
    }

  }
  environment 'HRS_API_SERVER_PORT', '8081'
  environment 'RSE_LIB_ADDITIONAL_DATABASES', 'emhrs'
  environment 'CCD_DOCUMENT_URL_PATTERN', 'https?://((dm-store:8080/documents/[A-Za-z0-9-]+(?:/binary)?)|(localhost:8081/hearing-recordings/[0-9a-fA-F]{8}-[0-9a-fA-F]{4}-[0-9a-fA-F]{4}-[0-9a-fA-F]{4}-[0-9a-fA-F]{12}/((segments\\/[0-9]+)|(file/\\S+))))'
  environment 'DATA_STORE_S2S_AUTHORISED_SERVICES', 'ccd_gw,ccd_data,ccd_ps,aac_manage_case_assignment,ccd_case_document_am_api,am_role_assignment_service,hmc_cft_hearing_service,prl_citizen_frontend,em_gw,em_hrs_api'
  authMode = uk.gov.hmcts.rse.AuthMode.Local
}

serenity {
  reports = ["single-page-html"]
}

pact {
  publish {
    pactDirectory = 'pacts'
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
    version = project.pactVersion
  }
}

pactPublish {
  pactPublish = project.pact.publish
}

compileJava.dependsOn processResources

pitest {
  targetClasses = ['uk.gov.hmcts.reform.em.hrs.util.*', 'uk.gov.hmcts.reform.em.hrs.service.*', 'uk.gov.hmcts.reform.em.hrs.controller.*', 'uk.gov.hmcts.reform.em.hrs.repository.*', 'uk.gov.hmcts.reform.em.hrs.storage.*']
  targetTests = ['uk.gov.hmcts.reform.em.hrs.util.*', 'uk.gov.hmcts.reform.em.hrs.service.*', 'uk.gov.hmcts.reform.em.hrs.controller.*', 'uk.gov.hmcts.reform.em.hrs.repository.*', 'uk.gov.hmcts.reform.em.hrs.storage.*']
  enableDefaultIncrementalAnalysis = true
  historyInputLocation = 'build/reports/pitest/fastermutationtesting'
  historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
  threads = 15
  testSourceSets = [sourceSets.test, sourceSets.integrationTest]
  mainSourceSets = [sourceSets.main]
  fileExtensionsToFilter.addAll('xml', 'json')
  outputFormats = ['XML', 'HTML', 'CSV']
  mutationThreshold = 0
  coverageThreshold = 0
  jvmArgs = ['-Xms1G', '-Xmx3G']
  timestampedReports = false
  failWhenNoMutations = false
  detectInlinedCode = true
  junit5PluginVersion = '1.2.1'
  pitestVersion = '1.17.3'
}

// exclude the logback binding is coming in via cftlib
configurations.cftlibTestImplementation {
  exclude group: 'ch.qos.logback', module: 'logback-classic'
}

flyway {
  url = System.getenv('FLYWAY_URL')
  user = System.getenv('FLYWAY_USER')
  password = "'" + System.getenv('FLYWAY_PASSWORD') + "'"
  baselineOnMigrate = true
  baselineVersion = '1'
}

task migratePostgresDatabase(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
}

task fortifyScan(type: JavaExec)  {
  main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}
